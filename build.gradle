plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'fun.lewisdev'
version = '4.0.0'
description = 'DeluxeHub'


repositories {
    mavenCentral()
    maven { url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/') }
    maven { url = uri('https://oss.sonatype.org/content/groups/public/') }
    maven { url = uri('https://jitpack.io') }
    maven { url = uri('https://repo.codemc.org/repository/maven-public') }
    maven { url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/') }
    maven { url = uri('https://libraries.minecraft.net/') }
    maven { url = uri('https://repo.maven.apache.org/maven2/') }
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }

}

dependencies {
    compileOnly 'com.github.shynixn.headdatabase:hdb-api:1.0'

    compileOnly 'net.md-5:bungeecord-chat:1.16-R0.1'
    compileOnly 'com.mojang:authlib:1.5.21'
    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'

    // Shaded libraries
    implementation 'co.aikar:acf-paper:0.5.1-SNAPSHOT'
    implementation 'de.tr7zw:item-nbt-api:2.12.2'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    implementation fileTree(dir: 'libs', include: '*.jar')
}

tasks.named('compileJava') {
    options.fork = true
    options.compilerArgs += ["-parameters"]
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

build {
    dependsOn(shadowJar)
}

shadowJar {
    minimize()
    archiveFileName = "DeluxeHub-${project.version}.jar"
    relocate 'net.zithium.library', 'fun.lewisdev.deluxehub.libs.library' // ZithiumLibrary
    relocate 'co.aikar.commands', 'fun.lewisdev.deluxehub.libs.acf' // Aikar Command Library
    relocate 'co.aikar.locales', 'fun.lewisdev.deluxehub.libs.locales' // Aikar Command Library
    relocate 'de.tr7zw.changeme.nbtapi', 'fun.lewisdev.deluxehub.libs.nbtapi' // NBTAPI
    relocate 'org.bstats', 'fun.lewisdev.deluxehub.libs.bstats' // bStats
}